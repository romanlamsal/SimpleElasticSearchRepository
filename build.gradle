plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'net.researchgate.release' version '2.6.0'
}

group 'de.lamsal'
version '0.1'

apply plugin: 'kotlin'

repositories {
    jcenter()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        kotlin.srcDir file('src/integrationTest/kotlin')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.9'
    implementation 'com.github.kittinunf.fuel:fuel:2.1.0'

    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation 'org.mock-server:mockserver-netty:5.5.4'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'

    integrationTestImplementation "org.testcontainers:testcontainers:1.11.3"
    integrationTestImplementation "org.testcontainers:elasticsearch:1.11.3"
}

test {
    useJUnitPlatform()
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task integrationTest(type: Test) {
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()
}